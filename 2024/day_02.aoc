data = []
while (true) {
    line = input()
    if (is_null(line)) {
        break
    }

    line = split(line, " ")
    for (i = 0; i < len(line); i = i + 1) {
        line[i] = int(line[i])
    }

    push(data, line)
}

abs = fn(n) {
    if (n < 0) {
        return -n
    }

    return n
}

ord = fn(n) {
    if (n > 0) {
        return 1
    } else if (n == 0) {
        return 0
    } else if (n < 0) {
        return -1
    }
}

wrong_idx = fn(line) {
    diff = line[1] - line[0]
    if (abs(diff) < 1 | abs(diff) > 3) {
        return 0
    }

    for (i = 2; i < len(line); i = i + 1) {
        diff2 = line[i] - line[i-1]
        if (abs(diff2) < 1 | abs(diff2) > 3) {
            return i-1
        }

        if (ord(diff) * ord(diff2) != 1) {
            return i-1
        }
    }

    return null
}

sum = 0
for (i = 0; i < len(data); i = i + 1) {
    if (is_null(wrong_idx(data[i]))) {
        sum = sum + 1
    }
}

print("Part one: " + str(sum))


// --------
// Part two
// --------

sum = 0
for (i = 0; i < len(data); i = i + 1) {
    line = data[i]
    idx = wrong_idx(line)
    if (is_null(idx)) {
        sum = sum + 1
        continue
    }

    for (j = 0; j < len(line); j = j + 1) {
        new_data = []
        for (k = 0; k < len(line); k = k + 1) {
            if (k == j) {
                continue
            }

            push(new_data, line[k])
        }

        if (is_null(wrong_idx(new_data))) {
            sum = sum + 1
            break
        }
    }
}

print("Part two: " + str(sum))
